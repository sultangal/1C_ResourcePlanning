#Область ОбработчикиСобытийФормы
&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ИнициализацияКалендаря();
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы
&НаКлиенте
Процедура ГодПриИзменении(Элемент)
	ИнициализацияКалендаря();
КонецПроцедуры

&НаКлиенте
Процедура КалендарьПриВыводеПериода(Элемент, ОформлениеПериода)
	Для Каждого Дата Из ОформлениеПериода.Даты Цикл
		Если ДеньНедели(Дата.Дата) = 6 Или ДеньНедели(Дата.Дата) = 7 Тогда
			Дата.ЦветФона = WebЦвета.БледноМиндальный;
		КонецЕсли;
	КонецЦикла;
	Для Каждого Строка Из Объект.ТаблицаИзмененийКалендаря Цикл
		Дата = ОформлениеПериода.Даты.Получить(Строка.Дата);
		Если Дата <> Неопределено Тогда
			Если Строка.СтатусДня = ПредопределенноеЗначение("Перечисление.СтатусДня.Нерабочий") Тогда
				Дата.Значение.ЦветФона = WebЦвета.БледноМиндальный;
			ИначеЕсли Строка.СтатусДня = ПредопределенноеЗначение("Перечисление.СтатусДня.Рабочий") Тогда
				Дата.Значение.ЦветФона = WebЦвета.Белый;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура Рабочий(Команда)
	ДобавитьДеньВТаблицуИзмененийКалендаря(Истина);
	Элементы.Календарь.Обновить();
КонецПроцедуры

&НаКлиенте
Процедура Нерабочий(Команда)
	ДобавитьДеньВТаблицуИзмененийКалендаря(Ложь);
	Элементы.Календарь.Обновить();
КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции
&НаСервере
Процедура ДобавитьДеньВТаблицуИзмененийКалендаря(РабочийЛиДень)
	//Проверим есть ли уже значение в таблице
	ПризнакНаличияВТаблице = Ложь;
	Для Каждого Строка Из Объект.ТаблицаИзмененийКалендаря Цикл
		Если Строка.Дата = Объект.Календарь Тогда
			ПризнакНаличияВТаблице = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	Если Не ПризнакНаличияВТаблице Тогда
		Строка = Объект.ТаблицаИзмененийКалендаря.Добавить();
		Строка.Дата = Объект.Календарь;
	КонецЕсли;
	Если РабочийЛиДень Тогда
		Строка.СтатусДня = Перечисления.СтатусДня.Рабочий;
	Иначе
		Строка.СтатусДня = Перечисления.СтатусДня.Нерабочий;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ИнициализацияКалендаря()
	Элементы.Календарь.НачалоПериодаОтображения = НачалоГода(Объект.Год);
	Элементы.Календарь.КонецПериодаОтображения = КонецГода(Объект.Год);
КонецПроцедуры
#КонецОбласти